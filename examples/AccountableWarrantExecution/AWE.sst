Problem: AccountableWarrantExecution;
Principals: Source, Court, Decryptor, Auditor, Judge, Public;
Knowledge: msg:bytes@Source, 
           id:bytes@Source,
           id:bytes@Court,
           msk:mskeyi@Decryptor,
           ska:skey@Auditor,
           skc:skey@Court,
           skd:skey@Decryptor,
           mpk:mpkeyi@Source = pkibe(msk),
           pka:pkey@Court = pk(ska),
           pkc:pkey@Auditor = pk(skc),
           pkc:pkey@Decryptor = pk(skc),
           pkd:pkey@Auditor = pk(skd);
Types: bytes, pkey, skey, mpkeyi, mskeyi, skeyi, rand;
Functions: OK() -> bytes,
           APPROVE() -> bytes,
           REJECT() -> bytes,
           ZKPFAIL() -> bytes,
           pk(skey) -> pkey,
           enc(pkey, bytes, rand) -> bytes,
           dec(skey, bytes) -> bytes,
           sign(skey, bytes) -> bytes,
           checksign(bytes, pkey) -> bytes,
           pkibe(mskeyi) -> mpkeyi,
           encibe(mpkeyi, bytes, bytes) -> bytes,
           extract(mskeyi, bytes) -> skeyi,
           decibe(bytes, skeyi) -> bytes,
           blind(bytes, rand) -> bytes,
           pextract(mskeyi, bytes) -> bytes,
           bextract(rand, bytes) -> skeyi,
           pzk(bytes, rand) -> bytes,
           checkzkpok(bytes, bytes) -> bytes,
           pzkenc(bytes, rand) -> bytes,
           checkzkpokenc(bytes, bytes) -> bytes,
           encwrap(bytes, rand) -> bytes,
           encunwrap(bytes) -> <bytes, rand>,
           signwrap(bytes, bytes, bytes) -> bytes,
           signunwrap(bytes) -> <bytes, bytes, bytes>;
Equations: dec(k, enc(pk(k), m, r)) = m,
           checksign(sign(ssk, m), pk(ssk)) = m,
           decibe(encibe(pkibe(msk), idi, m), extract(msk, idi)) = m,
           bextract(r, pextract(msk, blind(idi, r))) = extract(msk, idi),
           checkzkpok(blind(idi, r), pzk(idi, r)) = OK(),
           checkzkpokenc(enc(pk(sk), idi, r), pzkenc(idi, r)) = OK(),
           encunwrap(encwrap(id, r)) = <id, r>,
           signunwrap(signwrap(a, c, z)) = <a, c, z>;
Formats: PKIBEFORM(mpkeyi), PKFORM(pkey);
Events:  SourceEnd(bytes),
         JudgeApprove(bytes),
         JudgeReject(bytes),
         CourtKill(bytes),
         DecryptorKill(bytes),
         AuditorKill(bytes),
         DecryptorZKPFail(bytes, bytes, bytes),
         CourtEnd(bytes, bytes);
Queries: event(SourceEnd(ct)),
         event(JudgeApprove(status)),
         event(JudgeReject(status)),
         event(JudgeApprove(status)) & event(JudgeReject(status)),

         # If protocol was killed, it was because Judge rejected the warrant or Decryptor check for ZKP failed
         event(CourtKill(status)) & event(DecryptorKill(status)) & event (AuditorKill(status)),
         event(CourtKill(status)) & event(DecryptorKill(status)) & event (AuditorKill(status)) => event(JudgeReject(status)) | event(DecryptorZKPFail(status, pkzenc, pzk)), 

         # If Court ended, it was with the same ciphertext as the one supplied by Source, and Judge approved the warrant
         event(CourtEnd(status, ct)),
         event(CourtEnd(status, ct)) & event(JudgeApprove(status)) => event(SourceEnd(ct));
Protocol:
Source -> Public: mpkform = PKIBEFORM<mpk>
Court -> Public: pkaform = PKFORM<pka>
Decryptor -> Public: pkcform = PKFORM<pkc>
Auditor -> Public: pkdform = PKFORM<pkd>
let G = {
    Judge *->* Court: status = status
    Court *->* Decryptor: status = status
    Court *->* Auditor: status = status
    Court {
        if (status = REJECT()) {
            event CourtKill(status);
            end
        }
    }
    Decryptor {
        if (status = REJECT()) {
            event DecryptorKill(status);
            end
        }
    }
    Auditor {
        if (status = REJECT()) {
            event AuditorKill(status);
            end
        }
    }
    Court {
        new r: rand;
        let ctc = enc(pka, id, r);
        let signed_ctc = sign(skc, ctc);
    }
    Court *->* Auditor: ctcsign = signed_ctc
    Court {
        new r2: rand;
        let audit = enc(pka, encwrap(id, r), r2);
        let signed_audit = sign(skc, audit);
        let b = blind(id, r);
    }
    Court *->* Decryptor: drequest = <b, pzk(id, r), signed_ctc, signed_audit, pzkenc(id, r)>
    Decryptor {
        let <b: bytes, pzk: bytes, signed_ctc: bytes, signed_audit: bytes, pzkenc: bytes> = drequest;
        let ctc = checksign(signed_ctc, pkc);
        let audit = checksign(signed_audit, pkc);
        if ((checkzkpokenc(ctc, pzkenc) = OK()) & (checkzkpok(b, pzk) = OK())) {
            let status = APPROVE();
        } else {
            let status = ZKPFAIL();
        }
    }
    Decryptor *->* Court: status = status
    Decryptor *->* Auditor: status = status
    Decryptor {
        if (status = ZKPFAIL()) {
            event DecryptorKill(status);
            end
        }
    }
    Court {
        if (status = ZKPFAIL()) {
            event CourtKill(status);
            end
        }
    }
    Auditor {
        if (status = ZKPFAIL()) {
            event AuditorKill(status);
            end
        }
    }
    Decryptor {
        let partial = pextract(msk, b);
        let dsign = sign(skd, signwrap(audit, ctc, pzkenc));
    }
    Decryptor *->* Court: partial = partial
    Decryptor *->* Auditor: dsign = dsign
    Court {
        let sk = bextract(r, partial);
        let m = decibe(ct, sk);
        event CourtEnd(status, ct);
    }
    Auditor -> Public: auditableproof1 = <ctcsign, dsign>
    Auditor {
        let ctc = checksign(ctcsign, pkc);
        let id_ctc = dec(ska, ctc);
        let <audit: bytes, ctcd: bytes, zkenc: bytes> = signunwrap(checksign(dsign, pkd));
        let <id_audit: bytes, r: rand> = encunwrap(dec(ska, audit));
    }
    Auditor -> Public: auditableproof2 = <audit, ctcd, zkenc>
    end
} in
Source {
    let ct = encibe(mpk, id, msg);
    event SourceEnd(ct);
}
Source *->* Court: ct = ct
Source -> Public: ct = ct
Judge {
    let status = APPROVE(); # Set the initial status as 'approve'
}
Court -> Judge {
    Approve:
        Judge {
            event JudgeApprove(status);
        }
        G()
    Reject:
        Judge {
            let status = REJECT();
            event JudgeReject(status);
        }
        G()
}
