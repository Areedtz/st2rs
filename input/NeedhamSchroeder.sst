Problem: NeedhamSchroeder;
Principals: A, B;
Knowledge: ida:id@A,
           idb:id@A,
           idb:id@B,
           ida:id@B,
           ska:skey@A,
           skb:skey@B,
           pkb:pkey@A = pk(skb),
           pka:pkey@B = pk(ska);
Types: id, bytes, skey, pkey;
Functions: pk(skey) -> pkey,
           aenc(pkey, bytes) -> bytes,
           adec(skey, bytes) -> bytes,
           nawrap(id, bytes) -> bytes,
           naunwrap(bytes) -> <id, bytes>,
           nanbwrap(id, bytes, bytes) -> bytes,
           nanbunwrap(bytes) -> <id, bytes, bytes>; 
Equations: adec(k, aenc(pk(k), m)) = m,
           naunwrap(nawrap(idp, nonce)) = <idp, nonce>,
           nanbunwrap(nanbwrap(idp, nonce1, nonce2)) = <idp, nonce1, nonce2>;
Events:  start_a(id, id, bytes),
         mid_a(id, id, bytes, bytes), 
         end_a(id, id, bytes, bytes),
         start_b(id, id, bytes, bytes),
         end_b(id, id, bytes, bytes);
Queries: event(start_a(ida, idb, na)),
         event(mid_a(ida, idb, na, nb)),
         event(end_a(ida, idb, na, nb)),
         event(start_b(ida, idb, na, nb)),
         event(end_b(ida, idb, na, nb)),
         event(end_a(ida, idb, na, nb)) => event(start_b(ida, idb, na, nb)),
         inj-event(end_a(ida, idb, na, nb)) => inj-event(start_b(ida, idb, na, nb));
Protocol:
A {
    new na: bytes;
    event start_a(ida, idb, na);
    let ct = aenc(pkb, nawrap(ida, na));
}
A -> B: cta = ct
B {
    let <%ida, na: bytes> = naunwrap(adec(skb, cta));
    new nb: bytes;
    event start_b(ida, idb, na, nb);
    let ct = aenc(pka, nanbwrap(idb, na, nb));
}
B -> A: ctb = ct
A {
    let <%idb, x_na: bytes, x_nb: bytes> = nanbunwrap(adec(ska, ctb));
    event mid_a(ida, idb, x_na, x_nb);
    new enc_nb: bytes;
    if (x_na = na) {
        let enc_nb = aenc(pkb, x_nb);
        event end_a(ida, idb, x_na, x_nb);
    }
}
A -> B: z = enc_nb
B {
    let z_nb = adec(skb, z);
    if (z_nb = nb) {
        event end_b(ida, idb, na, nb);
    }
}
end